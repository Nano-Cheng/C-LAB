Block(Name=Pal3_Centrifuge,Pal3Name=$System.DefaultPal3Name$,MoveHome=False)
    SetCallParameterDefaultValue(Name=Speed,Value=2600)
    SetCallParameterDefaultValue(Name=Rate,Value=400)
    SetCallParameterDefaultValue(Name=Time,Value=1)
    Call(Name=Pal3_CentrifugeResetError,CentriName=%CentriName%)
    Call(Name=Pal3_CentrifugeStop,CentriName=%CentriName%,Time=3)
    If(InsGetField('%Pal3Name%','ToolHandlingBehavior')=='Automatic')
        %Pal3Name%.ChangeTool(Tool=%Tool%)
    EndIf()
    Call(Name=Pal3_PPSDoorOpen,Pal3Name=%Pal3Name%,MoveHome=False)
    System.PrintMessage(Message="Centrifuge Move In Sample",FCColor=Blue,BGColor=White)
    Call(Name=Pal3_CentrifugeGoToPosition,Position=0,Pal3Name=%Pal3Name%,Tool=%Tool%,CentriName=%CentriName%,Type=$%CentriName%.GoToPosType$,MoveHome=False)
    Call(Name=Pal3_PPSMoveIn,Source=%Source%,SourceIndex=%SourceIndex%,Pal3Name=%Pal3Name%,MoveHome=%MoveHome%)
    //配平
    System.PrintMessage(Message="Centrifuge Move In BalanceVial",FCColor=Blue,BGColor=White)
    Call(Name=Pal3_CentrifugeGoToPosition,Position=1800,Pal3Name=%Pal3Name%,Tool=%Tool%,CentriName=%CentriName%,Type=$%CentriName%.GoToPosType$,MoveHome=False)
    Call(Name=Pal3_PPSMoveIn,Source=%Source2%,SourceIndex=%SourceIndex2%,Pal3Name=%Pal3Name%,MoveHome=False)
    Call(Name=Pal3_PPSDoorClose,Pal3Name=%Pal3Name%,MoveHome=False)
    Call(Name=Pal3_CentrifugeStart,CentriName=%CentriName%,Speed=%Speed%,Rate=%Rate%)
    System.Wait(Time={%Time%*60})
    Call(Name=Pal3_CentrifugeStop,CentriName=%CentriName%)
    Call(Name=Pal3_PPSDoorOpen,Pal3Name=%Pal3Name%,MoveHome=False)
    System.PrintMessage(Message="Centrifuge Move Out Sample",FCColor=Blue,BGColor=White)
    Call(Name=Pal3_CentrifugeGoToPosition,Position=0,Pal3Name=%Pal3Name%,Tool=%Tool%,CentriName=%CentriName%,Type=$%CentriName%.GoToPosType$,MoveHome=False)
    Call(Name=Pal3_PPSMoveOut,Source=%Source%,SourceIndex=%SourceIndex%,Pal3Name=%Pal3Name%,MoveHome=False)
    //配平
    System.PrintMessage(Message="Centrifuge Move Out BalanceVial",FCColor=Blue,BGColor=White)
    Call(Name=Pal3_CentrifugeGoToPosition,Position=1800,Pal3Name=%Pal3Name%,Tool=%Tool%,CentriName=%CentriName%,Type=$%CentriName%.GoToPosType$,MoveHome=False)
    Call(Name=Pal3_PPSMoveOut,Source=%Source2%,SourceIndex=%SourceIndex2%,Pal3Name=%Pal3Name%,MoveHome=False)
    Call(Name=Pal3_PPSDoorClose,Pal3Name=%Pal3Name%,MoveHome=True)
EndBlock()

Block(Name=Pal3_CentrifugeGoToPosition,Type=Cmd,Position=1800)
    If('%Type%'=='Tool')
        %CentriName%.GET_POS
        If(Abs(StrToDouble($%CentriName%.CmdAnswer$)-%Position%)>10)
            If(InsGetField('%Pal3Name%','ToolHandlingBehavior')=='Automatic')
                %Pal3Name%.ChangeTool(Tool=%Tool%)
            EndIf()
            %Pal3Name%.MoveRelative(ReferencePoint=Centrifuge_Pos,MovementX=0mm,MovementY=30mm,MovementZ=0mm,AccelerationFactor=60%,ForceDirectMovement=True)
            %Pal3Name%.MoveRelative(MovementX=0mm,MovementY=-34mm,MovementZ=0mm,AccelerationFactor=30%,ForceDirectMovement=True)
            System.Wait(Time=1)
            %Pal3Name%.MoveRelative(MovementX=0mm,MovementY=40mm,MovementZ=0mm,AccelerationFactor=30%,ForceDirectMovement=True)
            %Pal3Name%.GetAxisPosition(Axis=3)
            SetVar(VarRange=%Pal3Name%,VarName=_Z,VarValue={StrToDouble(StrReplace('$%Pal3Name%.AtomReturnInfo$',' m',''))*1000})
            %Pal3Name%.MoveRelative(MovementX=0mm,MovementY=0mm,MovementZ=-$%Pal3Name%._Z$mm,AccelerationFactor=45%,ForceDirectMovement=True)
            If('%MoveHome%'=='True')
                %Pal3Name%.MoveToHome()
            EndIf()
        EndIf()
    Else()
        %CentriName%.GOTO_POS %Position% 1 500 300
    EndIf()
EndBlock()
Block(Name=Pal3_CentrifugeResetError)
    If('$%CentriName%._Address$'!='127.0.0.1')
        %CentriName%.GET_STATUS
        If('$%CentriName%.CmdAnswer$'=='ERROR 12')
            %CentriName%.RESET_ERROR 12
        EndIf()
        If('$%CentriName%.CmdAnswer$'=='ERROR 13')
            %CentriName%.RESET_ERROR 13
        EndIf()
        If('$%CentriName%.CmdAnswer$'=='ERROR 14')
            %CentriName%.RESET_ERROR 14
        EndIf()
    EndIf()
EndBlock()

Block(Name=Pal3_CentrifugeStart)
    System.PrintMessage(Message="Centrifuge Start",FCColor=Blue,BGColor=White)
    If('$%CentriName%._Address$'!='127.0.0.1')
        %CentriName%.GET_STATUS
        If('$%CentriName%.CmdAnswer$'=='IDLE')
            %CentriName%.CENTRIFUGE %Speed% %Rate%
        Else()
            System.ErrorExit(ErrorInfo="Centrifuge is $%CentriName%.CmdAnswer$, can't start!")
        EndIf()
        System.Wait(Time=5)
        %CentriName%.GET_STATUS
        If('$%CentriName%.CmdAnswer$'!='WORKING')
            System.ErrorExit(ErrorInfo="Centrifuge is $%CentriName%.CmdAnswer$, start error!")
        EndIf()
    EndIf()
EndBlock()

Block(Name=Pal3_CentrifugeStop,CentriName=PPS,Time=15)
    System.PrintMessage(Message="Centrifuge Stop",FCColor=Blue,BGColor=White)
    If('$%CentriName%._Address$'!='127.0.0.1')
        %CentriName%.GET_STATUS
        If('$%CentriName%.CmdAnswer$'=='WORKING')
            %CentriName%.STOP
        EndIf()
        System.Wait(Time=%Time%)
        %CentriName%.GET_STATUS
        If('$%CentriName%.CmdAnswer$'!='IDLE')
            System.ErrorExit(ErrorInfo="Centrifuge is $%CentriName%.CmdAnswer$, stop error!")
        EndIf()
    EndIf()
EndBlock()

Block(Name=Pal3_PPSDoorOpen,Pal3Name=$System.DefaultPal3Name$,MoveHome=True)
    System.PrintMessage(Message="Centrifuge Open Door",FCColor=Blue,BGColor=White)
    %Pal3Name%.MoveToObject(Target=Centrifuge_Open,Index=1,UseTouchDown=False)
    %Pal3Name%.MoveRelative(MovementX=0mm,MovementY=-60mm,MovementZ=0mm,AccelerationFactor=60%,ForceDirectMovement=True)
    %Pal3Name%.MoveRelative(MovementX=0mm,MovementY=5mm,MovementZ=0mm,AccelerationFactor=60%,ForceDirectMovement=True)
    %Pal3Name%.GetAxisPosition(Axis=3)
    SetVar(VarRange=%Pal3Name%,VarName=_Z,VarValue={StrToDouble(StrReplace('$%Pal3Name%.AtomReturnInfo$',' m',''))*1000})
    %Pal3Name%.MoveRelative(MovementX=0mm,MovementY=0mm,MovementZ=-$%Pal3Name%._Z$mm,AccelerationFactor=45%,ForceDirectMovement=True)
    If('%MoveHome%'=='True')
        %Pal3Name%.MoveToHome()
    EndIf()
EndBlock()

Block(Name=Pal3_PPSDoorClose,Pal3Name=$System.DefaultPal3Name$,MoveHome=True)
    System.PrintMessage(Message="Centrifuge Close Door",FCColor=Blue,BGColor=White)
    %Pal3Name%.MoveToObject(Target=Centrifuge_Close,Index=1,UseTouchDown=False)
    %Pal3Name%.MoveRelative(MovementX=0mm,MovementY=60mm,MovementZ=0mm,AccelerationFactor=60%,ForceDirectMovement=True)
    %Pal3Name%.MoveRelative(MovementX=0mm,MovementY=-5mm,MovementZ=0mm,AccelerationFactor=60%,ForceDirectMovement=True)
    %Pal3Name%.GetAxisPosition(Axis=3)
    SetVar(VarRange=%Pal3Name%,VarName=_Z,VarValue={StrToDouble(StrReplace('$%Pal3Name%.AtomReturnInfo$',' m',''))*1000})
    %Pal3Name%.MoveRelative(MovementX=0mm,MovementY=0mm,MovementZ=-$%Pal3Name%._Z$mm,AccelerationFactor=45%,ForceDirectMovement=True)
    If('%MoveHome%'=='True')
        %Pal3Name%.MoveToHome()
    EndIf()
EndBlock()

Block(Name=Pal3_PPSMoveIn,Pal3Name=$System.DefaultPal3Name$,MoveHome=True)
    System.PrintMessage(Message="Centrifuge Move In",FCColor=Blue,BGColor=White)
    %Pal3Name%.MoveToObject(Target=%Source%,Index=%SourceIndex%,UseTouchDown=True)
    %Pal3Name%.GetAxisPosition(Axis=3)
    SetVar(VarRange=%Pal3Name%,VarName=_Z,VarValue={StrToDouble(StrReplace('$%Pal3Name%.AtomReturnInfo$',' m',''))*1000})
    %Pal3Name%.MoveRelative(MovementX=0mm,MovementY=0mm,MovementZ=-$%Pal3Name%._Z$mm,AccelerationFactor=45%,ForceDirectMovement=True)
    %Pal3Name%.MoveRelative(ReferencePoint=Centrifuge_Vial,MovementX=0mm,MovementY=0mm,MovementZ=-1mm,AccelerationFactor=60%,ForceDirectMovement=True)
    %Pal3Name%.MoveRelative(MovementX=0mm,MovementY=0mm,MovementZ=-19mm,AccelerationFactor=60%,ForceDirectMovement=True)
    %Pal3Name%.MoveRelative(MovementX=0mm,MovementY=25mm,MovementZ=0mm,AccelerationFactor=60%,ForceDirectMovement=True)
    %Pal3Name%.GetAxisPosition(Axis=3)
    SetVar(VarRange=%Pal3Name%,VarName=_Z,VarValue={StrToDouble(StrReplace('$%Pal3Name%.AtomReturnInfo$',' m',''))*1000})
    %Pal3Name%.MoveRelative(MovementX=0mm,MovementY=0mm,MovementZ={-1*($%Pal3Name%._Z$-20)}mm,AccelerationFactor=45%,ForceDirectMovement=True)
    %Pal3Name%.LeaveObject()
    If('%MoveHome%'=='True')
        %Pal3Name%.MoveToHome()
    EndIf()
EndBlock()

Block(Name=Pal3_PPSMoveOut,Pal3Name=$System.DefaultPal3Name$,MoveHome=True)
    System.PrintMessage(Message="Centrifuge Move Out",FCColor=Blue,BGColor=White)
    %Pal3Name%.MoveRelative(ReferencePoint=Centrifuge_Vial,MovementX=0mm,MovementY=0mm,MovementZ=-1mm,AccelerationFactor=60%,ForceDirectMovement=True)
    %Pal3Name%.GetAxisPosition(Axis=3)
    SetVar(VarRange=%Pal3Name%,VarName=_Z,VarValue={StrToDouble(StrReplace('$%Pal3Name%.AtomReturnInfo$',' m',''))*1000})
    %Pal3Name%.MoveRelative(MovementX=0mm,MovementY=0mm,MovementZ={-1*($%Pal3Name%._Z$-20)}mm,AccelerationFactor=45%,ForceDirectMovement=True)
    %Pal3Name%.MoveToObject(Target=%Source%,Index=%SourceIndex%,UseTouchDown=True)
    %Pal3Name%.LeaveObject()
    %Pal3Name%.GetAxisPosition(Axis=3)
    SetVar(VarRange=%Pal3Name%,VarName=_Z,VarValue={StrToDouble(StrReplace('$%Pal3Name%.AtomReturnInfo$',' m',''))*1000})
    %Pal3Name%.MoveRelative(MovementX=0mm,MovementY=0mm,MovementZ=-$%Pal3Name%._Z$mm,AccelerationFactor=45%,ForceDirectMovement=True)
    If('%MoveHome%'=='True')
        %Pal3Name%.MoveToHome()
    EndIf()
EndBlock()
// vim: nowrap tw=0 ft=prepmacro
