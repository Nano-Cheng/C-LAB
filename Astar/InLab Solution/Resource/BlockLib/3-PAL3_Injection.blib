Block(Name=Pal3_TriggerOptic)
    //Optic有两种触发方式1
    //1通过Optic AUX自我触发，这情况，触发以后还得在适当时机主动触发GC
    //2通过原厂的PAL3线触发，这种情况触发完Optic后，GC由Optic自行触发
    If('$System.OpticTriggerMode$'=='Aux')
        //软触发时GC与Optic之间没有联系
        If(InsVarExist('System','_Instrument.OpticPTV') && StrContains('%Injector%','$System.OpticInjectorName$'))
            $System.DefaultOpticPTV$.SetDirectControlParameter(Name=Aux. Output 3,Value=1)
            System.Wait(Time=1)
            $System.DefaultOpticPTV$.SetDirectControlParameter(Name=Aux. Output 3,Value=0)
        EndIf()
    EndIf()
    If('$System.OpticTriggerMode$'=='Soft')
        //软触发时GC与Optic之间没有联系 用软件RunMethod
        If(InsVarExist('System','_Instrument.OpticPTV') && StrContains('%Injector%','$System.OpticInjectorName$'))
            $System.DefaultOpticPTV$.RunMethod()
        EndIf()
    EndIf()
EndBlock()

Block(Name=Pal3_LiquidInj,Pal3Name=$System.DefaultPal3Name$,InjTool=LS 1,Injector=Injector 1,ReadySignal=Input Signal 1,OutStartSignal=Output Signal 1,FillStrokeCount=3,GetSpeed=1,GetDeep=-1)
//::Source SourceIndex Volume
//:::Pal3Name  InjTool Injector ReadySignal OutStartSignal FillStrokeCount GetSpeed GetDelay InjSpeed PreInjDelay PostInjDelay
    SetCallParameterDefaultValue(Name=GetDelay,Value=500)
    SetCallParameterDefaultValue(Name=InjSpeed,Value=50)
    SetCallParameterDefaultValue(Name=PreInjDelay,Value=500)
    SetCallParameterDefaultValue(Name=PostInjDelay,Value=2000)
    SetCallParameterDefaultValue(Name=GCInjFillStrokeExtraVol,Value={InsGetField('%Pal3Name%','GCInjFillStrokeExtraVol')})
    SetCallParameterDefaultValue(Name=VialType,Value=None)
    SetCallParameterDefaultValue(Name=InjDepth,Value=-1)

    If('$%Pal3Name%.ToolHandlingBehavior$'=='Automatic')
        %Pal3Name%.ChangeTool(Tool=%InjTool%)
    EndIf()
    /*If('$%Pal3Name%.OnHeadTool$'!='%InjTool%' && 'Sample.FakeInjection'!='True' && !StrStartWith('$%Pal3Name%._Address$','127.0.0.1'))
        System.ErrorExit(ErrorInfo="ChangeTool $InjTool$ Failed ToolHandlingBehavior $%Pal3Name%.ToolHandlingBehavior$ OnHeadTool $%Pal3Name%.OnHeadTool$ InjTool %InjTool%")
    EndIf()*/
    If('$Sample._SandwitchInj$'=='True')
        If($Sample._RearSolventVol$>0)
            %Pal3Name%.MoveToObject(Target=$Sample._RearSolvent_Tray$,Index=$Sample._RearSolvent_Index$)
            %Pal3Name%.PenetrateObject(Target=$Sample._RearSolvent_Tray$,Index=$Sample._RearSolvent_Index$)
            %Pal3Name%.AspirateSyringe(Volume=$Sample._RearSolventVol$uL,FlowRate=%GetSpeed%uL/s)
            %Pal3Name%.LeaveObject()
        EndIf()
        If($Sample._RearAirVol$>0)
            %Pal3Name%.AspirateSyringe(Volume=$Sample._RearAirVol$uL,FlowRate=%GetSpeed%uL/s)
        EndIf()
        If($Sample._RearInnerVol$>0)
            %Pal3Name%.MoveToObject(Target=$Sample._RearInner_Tray$,Index=$Sample._RearInner_Index$)
            %Pal3Name%.PenetrateObject(Target=$Sample._RearInner_Tray$,Index=$Sample._RearInner_Index$)
            %Pal3Name%.AspirateSyringe(Volume=$Sample._RearInnerVol$uL,FlowRate=%GetSpeed%uL/s)
            %Pal3Name%.LeaveObject()
        EndIf()
        If($Sample._MidAirVol$>0)
            %Pal3Name%.AspirateSyringe(Volume=$Sample._MidAirVol$uL,FlowRate=%GetSpeed%uL/s)
        EndIf()
    EndIf()

    If('%VialType%'=='FlipTube')
        %Pal3Name%.ApproachObject(Target=%Source%,Index=%SourceIndex%,OffsetX=0mm,OffsetY=12mm,OffsetZ=-5mm)
        %Pal3Name%.MoveRelative(MovementY=-17mm,AccelerationFactor=40%,DrfOption=1,ForceDirectMovement=True)
        %Pal3Name%.Wait(Time=2s)
        %Pal3Name%.MoveRelative(MovementY=5mm,AccelerationFactor=40%,DrfOption=1,ForceDirectMovement=True)
        %Pal3Name%.MoveNeedleGuide(Movement=45mm,AccelerationFactor=50%,IsRelativeMovement=False)
        %Pal3Name%.MoveRelative(MovementZ=40mm,AccelerationFactor=40%,DrfOption=1,ForceDirectMovement=True)
        %Pal3Name%.FillingStrokes(Volume={%Volume%+%GCInjFillStrokeExtraVol%}uL,AspirateFlowRate=%GetSpeed%uL/s,Count=%FillStrokeCount%)
        %Pal3Name%.AspirateSyringe(Volume=%Volume%uL,FlowRate=%GetSpeed%uL/s,PullupDelay=%GetDelay%ms)
        %Pal3Name%.MoveRelative(MovementZ=-50mm,AccelerationFactor=40%,DrfOption=1,ForceDirectMovement=True)
        %Pal3Name%.MoveNeedleGuide(Movement=0mm,AccelerationFactor=50%,IsRelativeMovement=False)
        %Pal3Name%.LeaveObject()
    Else()
        If(ListContains('$%Pal3Name%.TraySlots$','%Source%'))
            %Pal3Name%.MoveToObject(Target=%Source%,Index=%SourceIndex%)
            If(%GetDeep%==-1)
                %Pal3Name%.PenetrateObject(Target=%Source%,Index=%SourceIndex%)
            EndIf()
            If(%GetDeep%==-2)
                %Pal3Name%.PenetrateWithBottomSense(Target=%Source%,Index=%SourceIndex%,HeightFromBottom=200um)
            EndIf()
            If(%GetDeep%>0)
                %Pal3Name%.PenetrateObject(Target=%Source%,Index=%SourceIndex%,Depth=%GetDeep%mm)
            EndIf()
        Else()
            %Pal3Name%.MoveToObject(Target=%Source%,Index=%SourceIndex%)
            %Pal3Name%.PenetrateObject(Target=%Source%,Index=%SourceIndex%)
        EndIf()
        %Pal3Name%.FillingStrokes(Volume={%Volume%+%GCInjFillStrokeExtraVol%}uL,AspirateFlowRate=%GetSpeed%uL/s,Count=%FillStrokeCount%)
        %Pal3Name%.AspirateSyringe(Volume=%Volume%uL,FlowRate=%GetSpeed%uL/s,PullupDelay=%GetDelay%ms)
        %Pal3Name%.LeaveObject()
        If('$Sample._SandwitchInj$'=='True')
            If($Sample._FrontAirVol$>0)
                %Pal3Name%.AspirateSyringe(Volume=$Sample._FrontAirVol$uL,FlowRate=%GetSpeed%uL/s)
            EndIf()
        EndIf()
    EndIf()

    SetVar(VarRange=%Pal3Name%,VarName=%InjTool%.Solvent,VarValue=%Source%#%SourceIndex%)
    SetVar(VarRange=%Pal3Name%,VarName=%InjTool%.DirtyVolume,VarValue={%Volume%+3})
    SetVar(VarRange=%Pal3Name%,VarName=Plugin_Injected,VarValue=True)
    If('%OutStartSignal%'=='None')
        If(InsVarExist('%Pal3Name%','FastInjection') && InsGetField('%Pal3Name%','FastInjection')=='True')
            If(%InjDepth%==-1)
                %Pal3Name%.FastInjectSampleGC(Injector=%Injector%)
            Else()
                %Pal3Name%.FastInjectSampleGC(Injector=%Injector%,PenetrationDepth=%InjDepth%mm)
            EndIf()
        Else()
            If(StrContains('$SL.Injector$','$System.OpticInjectorName$') && StrContains('$SL.Injector$','LNX'))
                %Pal3Name%.InjectSampleGC(Injector=%Injector%,PreDelay=%PreInjDelay%ms,PostDelay=%PostInjDelay%ms,FlowRate=%InjSpeed%uL/s,PenetrationDepth=32mm)
            Else()
                If(%InjDepth%==-1)
                    %Pal3Name%.InjectSampleGC(Injector=%Injector%,PreDelay=%PreInjDelay%ms,PostDelay=%PostInjDelay%ms,FlowRate=%InjSpeed%uL/s)
                Else()
                    %Pal3Name%.InjectSampleGC(Injector=%Injector%,PreDelay=%PreInjDelay%ms,PostDelay=%PostInjDelay%ms,FlowRate=%InjSpeed%uL/s,PenetrationDepth=%InjDepth%mm)
                EndIf()
            EndIf()
        EndIf()
    Else()
        If(InsVarExist('%Pal3Name%','FastInjection') && InsGetField('%Pal3Name%','FastInjection')=='True')
            If(%InjDepth%==-1)
                %Pal3Name%.FastInjectSampleGC(Injector=%Injector%,InjectedSignal=%OutStartSignal%)
            Else()
                %Pal3Name%.FastInjectSampleGC(Injector=%Injector%,InjectedSignal=%OutStartSignal%,PenetrationDepth=%InjDepth%mm)
            EndIf()
        Else()
            If(StrContains('$SL.Injector$','$System.OpticInjectorName$') && StrContains('$SL.Injector$','LNX'))
                %Pal3Name%.InjectSampleGC(Injector=%Injector%,InjectedSignal=%OutStartSignal%,PreDelay=%PreInjDelay%ms,PostDelay=%PostInjDelay%ms,FlowRate=%InjSpeed%uL/s,PenetrationDepth=32mm)
            Else()
                If(%InjDepth%==-1)
                    %Pal3Name%.InjectSampleGC(Injector=%Injector%,InjectedSignal=%OutStartSignal%,PreDelay=%PreInjDelay%ms,PostDelay=%PostInjDelay%ms,FlowRate=%InjSpeed%uL/s)
                Else()
                    %Pal3Name%.InjectSampleGC(Injector=%Injector%,InjectedSignal=%OutStartSignal%,PreDelay=%PreInjDelay%ms,PostDelay=%PostInjDelay%ms,FlowRate=%InjSpeed%uL/s,PenetrationDepth=%InjDepth%mm)
                EndIf()
            EndIf()
        EndIf()
        SetVar(VarRange=%Pal3Name%,VarName=Plugin_Injected,VarValue=False)
        Call(Name=Pal3_TriggerOptic,Injector=%Injector%)
    EndIf()
    %Pal3Name%.LeaveObject()
    %Pal3Name%.MoveToHome()
EndBlock()

Block(Name=Pal3_LCInj,Pal3Name=$System.DefaultPal3Name$,IfWaitSync=True)
//::Source SourceIndex Volume
//:::Pal3Name  InjTool Injector ReadySignal OutStartSignal FillStrokeCount GetSpeed GetDelay InjSpeed PreInjDelay PostInjDelay
    SetCallParameterDefaultValue(Name=InjTool,Value=LS 1)
    SetCallParameterDefaultValue(Name=Injector,Value=LcInjectorValve1)
    SetCallParameterDefaultValue(Name=ReadySignal,Value=Input Signal 1)
    SetCallParameterDefaultValue(Name=OutStartSignal,Value=Output Signal 1)
    SetCallParameterDefaultValue(Name=FillStrokeCount,Value=5)
    SetCallParameterDefaultValue(Name=IfBottomSense,Value=False)
    SetCallParameterDefaultValue(Name=Airgap,Value=0)
    SetCallParameterDefaultValue(Name=FrontVol,Value=0)
    SetCallParameterDefaultValue(Name=RearVol,Value=0)
    SetCallParameterDefaultValue(Name=GetSpeed,Value=1)
    SetCallParameterDefaultValue(Name=GetDelay,Value=500)
    SetCallParameterDefaultValue(Name=InjSpeed,Value=50)
    SetCallParameterDefaultValue(Name=PreInjDelay,Value=500)
    SetCallParameterDefaultValue(Name=PostInjDelay,Value=2000)
    SetCallParameterDefaultValue(Name=WashSource,Value=Fast Wash 1)
    SetCallParameterDefaultValue(Name=Wash1Index,Value=1)
    SetCallParameterDefaultValue(Name=Wash2Index,Value=2)

    If('$%Pal3Name%.ToolHandlingBehavior$'=='Automatic')
        %Pal3Name%.ChangeTool(Tool=%InjTool%)
    EndIf()

    /*If('$%Pal3Name%.OnHeadTool$'!='%InjTool%' && 'Sample.FakeInjection'!='True' && !StrStartWith('$%Pal3Name%._Address$','127.0.0.1'))
        System.ErrorExit(ErrorInfo="ChangeTool $InjTool$ Failed ToolHandlingBehavior $%Pal3Name%.ToolHandlingBehavior$ OnHeadTool $%Pal3Name%.OnHeadTool$ InjTool %InjTool%")
    EndIf()*/
    //空气
    %Pal3Name%.MoveToHome()
    %Pal3Name%.AspirateSyringe(Volume=%Airgap%uL)
    //取样
    %Pal3Name%.MoveToObject(Target=%Source%,Index=%SourceIndex%)
    If('%IfBottomSense%'=='True')
        %Pal3Name%.PenetrateWithBottomSense(Target=%Source%,Index=%SourceIndex%,HeightFromBottom=200um)
    Else()
        %Pal3Name%.PenetrateObject(Target=%Source%,Index=%SourceIndex%)
    EndIf()
    %Pal3Name%.AspirateSyringe(Volume={%FrontVol%+%Volume%+%RearVol%}uL,FlowRate=%GetSpeed%uL/s,PullupDelay=%GetDelay%ms)
    %Pal3Name%.LeaveObject()
    %Pal3Name%.AspirateSyringe(Volume=%Airgap%uL)

    %Pal3Name%.MoveToObject(Target=%Injector%)
    If('%IfWaitSync%'=='True')
        %Pal3Name%.WaitForSyncSignal(Signal=%ReadySignal%)
    EndIf()
    %Pal3Name%.PenetrateWithConstForce(Target=%Injector%)
    %Pal3Name%.MoveInjectorValve(Target=%Injector%,Position=2)
    %Pal3Name%.DispenseSyringe(Volume={%FrontVol%+%Airgap%}uL,FlowRate=10uL/s)
    If('%OutStartSignal%'=='None')
        %Pal3Name%.InjectSampleLC(Target=%Injector%,Volume=%Volume%uL,PreDelay=%PreInjDelay%ms,PostDelay=%PostInjDelay%ms,FlowRate=%InjSpeed%uL/s)
        %Pal3Name%.MoveInjectorValve(Target=%Injector%,Position=1)
    Else()
        %Pal3Name%.InjectSampleLC(Target=%Injector%,InjectedSignal1=%OutStartSignal%,Volume=%Volume%uL,PreDelay=%PreInjDelay%ms,PostDelay=%PostInjDelay%ms,FlowRate=%InjSpeed%uL/s)
    EndIf()
    %Pal3Name%.EmptySyringe(FlowRate=10uL/s)
    %Pal3Name%.LeaveObject()
    If(%PostVlvClean1Vol%>0)
        %Pal3Name%.CleanInjector(Target=%Injector%,WashSource=%WashSource%,WashIndex=%Wash1Index%,WashVolume=%PostVlvClean1Vol%uL,LeaveObjectAfterClean=True) 
    EndIf()
    If(%PostVlvClean2Vol%>0)
        If(%Wash2Index%!=-1)
            %Pal3Name%.CleanInjector(Target=%Injector%,WashSource=%WashSource%,WashIndex=%Wash2Index%,WashVolume=%PostVlvClean2Vol%uL,LeaveObjectAfterClean=True) 
        EndIf()
    EndIf()
    %Pal3Name%.MoveToHome()
EndBlock()

Block(Name=Pal3_HSInj,Pal3Name=$System.DefaultPal3Name$)
//::Volume InjTool InjToolTemp Source SourceIndex
//:::Pal3Name Injector Agitator GetDeep GetSpeed InjSpeed
    SetCallParameterDefaultValue(Name=InjTool,Value=HS 1)
    SetCallParameterDefaultValue(Name=Injector,Value=Injector 1)
    SetCallParameterDefaultValue(Name=AgitatorName,Value=Agitator 1)
    SetCallParameterDefaultValue(Name=ReadySignal,Value=Input Signal 1)
    SetCallParameterDefaultValue(Name=OutStartSignal,Value=Output Signal 1)
    SetCallParameterDefaultValue(Name=PurgeTime,Value=30)
    SetCallParameterDefaultValue(Name=GetDeep,Value=12)
    SetCallParameterDefaultValue(Name=GetSpeed,Value=450)
    SetCallParameterDefaultValue(Name=GetDelay,Value=1000)
    SetCallParameterDefaultValue(Name=InjSpeed,Value=500)
    SetCallParameterDefaultValue(Name=PreInjDelay,Value=500)
    SetCallParameterDefaultValue(Name=PostInjDelay,Value=3000)
    SetCallParameterDefaultValue(Name=IfFocus,Value=False)
    SetCallParameterDefaultValue(Name=FocusCount,Value=3)
    SetCallParameterDefaultValue(Name=IfHeat,Value=True)
    SetCallParameterDefaultValue(Name=InLabSMMName,Value=None)

    If('$%Pal3Name%.ToolHandlingBehavior$'=='Automatic')
        %Pal3Name%.ChangeTool(Tool=%InjTool%)
    EndIf()
    %Pal3Name%.SetTemperature(Target=%InjTool%.SyringeHeater,Temperature=%InjToolTemp%C,Wait=True)
    If('$System.WaitMassLynx$'=='True')
        System.WaitMassLynxReady(TimeOut=30,WaitString=$System.MassLynxReadyString$)
    EndIf()
    If('%ReadySignal%'!='None')
        If(InsVarExist('%Pal3Name%','SendPrep') && InsGetField('%Pal3Name%','SendPrep')=='True')
            If(InsGetField('%Pal3Name%','CheckReadySignalDouble')=='True')
            //8890
                System.Wait8890SendPrep(GCIP="$System.8890GCIP$")
                %Pal3Name%.SetSignal(Signal=Prep,Mode=2)
            Else()
                %Pal3Name%.SetScheduledSignal(Signal=Prep,Time=15s) 
            EndIf()
        EndIf()

        %Pal3Name%.WaitForSyncSignal(Signal=%ReadySignal%)
        If(InsGetField('%Pal3Name%','CheckReadySignalDouble')=='True')
            System.Wait(Time={InsGetField('%Pal3Name%','CheckReadySignalDoubleTime')},Info='8890 Double Check Ready Signal')
            %Pal3Name%.WaitForSyncSignal(Signal=%ReadySignal%)
        EndIf()
    EndIf()
    If(StrStartWith('%AgitatorName%','Agitator'))
        %Pal3Name%.PauseAgitator(Agitator=%AgitatorName%)
    EndIf()
    If(StrStartWith('%AgitatorName%','Heater Stirrer'))
        %Pal3Name%.PauseStirrer(Stirrer=%AgitatorName%)
    EndIf()
    //System.InfoDialog(Message="SMM Name %InLabSMMName%",ShowCancel=False)
    If('%IfFocus%'=='True')
        System.PrintMessage(Message="HeadSpace Focus Injection<%FocusCount%>",FCColor=Blue,BGColor=White)
        Repeat(%FocusCount%)
            If('%IfHeat%'=='True')
                %Pal3Name%.MoveToObject(Target=%Source%:%SourceIndex%)
                %Pal3Name%.PenetrateObject(Target=%Source%:%SourceIndex%,Depth=%GetDeep%mm)
            Else()
                %Pal3Name%.MoveToObject(Target=%Source%,Index=%SourceIndex%)
                %Pal3Name%.PenetrateObject(Target=%Source%,Index=%SourceIndex%,Depth=%GetDeep%mm)
            EndIf()
            %Pal3Name%.AspirateSyringe(Volume=%Volume%uL,FlowRate=%GetSpeed%uL/s,PullupDelay=%GetDelay%ms)
            %Pal3Name%.LeaveObject()
            If(StrContains('$SL.Injector$','$System.OpticInjectorName$') && StrContains('$SL.Injector$','LNX'))
                %Pal3Name%.InjectSampleGC(Injector=%Injector%,PreDelay=%PreInjDelay%ms,PostDelay=%PostInjDelay%ms,FlowRate=%InjSpeed%uL/s,PenetrationDepth=28mm)
            Else()
                %Pal3Name%.InjectSampleGC(Injector=%Injector%,PreDelay=%PreInjDelay%ms,PostDelay=%PostInjDelay%ms,FlowRate=%InjSpeed%uL/s)
            EndIf()
            %Pal3Name%.LeaveObject()
        EndRepeat()
        %Pal3Name%.SetSignal(Signal=%OutStartSignal%,Mode=2)
        If('%InLabSMMName%'!='None')
            %InLabSMMName%.Start()
        EndIf()
        Call(Name=Pal3_TriggerOptic,Injector=%Injector%)
        %Pal3Name%.MoveToHome()
    Else()
        If('%IfHeat%'=='True')
            %Pal3Name%.MoveToObject(Target=%Source%:%SourceIndex%)
            %Pal3Name%.PenetrateObject(Target=%Source%:%SourceIndex%,Depth=%GetDeep%mm)
        Else()
            %Pal3Name%.MoveToObject(Target=%Source%,Index=%SourceIndex%)
            %Pal3Name%.PenetrateObject(Target=%Source%,Index=%SourceIndex%,Depth=%GetDeep%mm)
        EndIf()
        %Pal3Name%.AspirateSyringe(Volume=%Volume%uL,FlowRate=%GetSpeed%uL/s,PullupDelay=%GetDelay%ms)
        %Pal3Name%.LeaveObject()
        SetVar(VarRange=%Pal3Name%,VarName=Plugin_Injected,VarValue=True)
        If(StrContains('$SL.Injector$','$System.OpticInjectorName$') && StrContains('$SL.Injector$','LNX'))
            %Pal3Name%.InjectSampleGC(Injector=%Injector%,InjectedSignal=%OutStartSignal%,PreDelay=%PreInjDelay%ms,PostDelay=%PostInjDelay%ms,FlowRate=%InjSpeed%uL/s,PenetrationDepth=28mm)
        Else()
            %Pal3Name%.InjectSampleGC(Injector=%Injector%,InjectedSignal=%OutStartSignal%,PreDelay=%PreInjDelay%ms,PostDelay=%PostInjDelay%ms,FlowRate=%InjSpeed%uL/s)
        EndIf()
        SetVar(VarRange=%Pal3Name%,VarName=Plugin_Injected,VarValue=False)
        Call(Name=Pal3_TriggerOptic,Injector=%Injector%)
        If('%InLabSMMName%'!='None')
            %InLabSMMName%.Start()
        EndIf()
        %Pal3Name%.LeaveObject()
        %Pal3Name%.MoveToHome()
    EndIf()

    If('%IfHeat%'=='True')
        If(StrStartWith('%AgitatorName%','Agitator'))
            %Pal3Name%.ResumeAgitator(Agitator=%AgitatorName%)
        EndIf()
        If(StrStartWith('%AgitatorName%','Heater Stirrer'))
            %Pal3Name%.ResumeStirrer(Agitator=%AgitatorName%)
        EndIf()
    EndIf()

    If(ListContains(InsGetProperty('%Pal3Name%','OutputSignalDescription'),'24GasValve'))
        %Pal3Name%.SetSignal(Signal=24GasValve,Mode=1)
    Else()
        %Pal3Name%.StartPurgeSyringe()
    EndIf()

    %Pal3Name%.Wait(Time=%PurgeTime%s)

    If(ListContains(InsGetProperty('%Pal3Name%','OutputSignalDescription'),'24GasValve'))
        %Pal3Name%.SetSignal(Signal=24GasValve,Mode=0)
    Else()
        %Pal3Name%.StopPurgeSyringe()
    EndIf()
EndBlock()
// vim: nowrap tw=0 ft=prepmacro
